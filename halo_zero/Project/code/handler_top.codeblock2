If PlasmaHeat > 0
    If PlasmaHeat >= 100
        Set PlasmaOverheated = True
    End    
    Set PlasmaHeat=PlasmaHeat - Frame_Adjust
    If PlasmaHeat <= 0
        Set PlasmaOverheated=False
    End
    Gosub refresh_ui
End

//If we are damaged
Variable word mc_xfacing=0
Variable word mc_x=0
Set mc_xfacing=actor_xfacing
Set mc_x=actor_x
If Shield < 75
    If ShieldDelay > 0
        Set ShieldDelay=ShieldDelay - 1
        If ShieldDelay == 0
            //We've just hit the End of the shield recharge time
            Sound shieldrecharge
        End
    Else
        Set Shield=Shield + 1
    End
    If Shield == 0
        Sound shieldlow
    End
End

If IsDucking
    Set actor_origin_y=-21
Else
    Set actor_origin_y=-39
End

If control1_buttons btst CD32_Green and Actor_AnimationTag == masterchief_top_idle
    Goto reload_start
ElseIf control1_buttons btst CD32_Yellow and Actor_AnimationTag == masterchief_top_idle
    Goto weapon_swap
ElseIf control1_buttons btst Amiga_Fire2 and Actor_AnimationTag == masterchief_top_idle and Grenade1+Grenade2 > 0
    If Grenade2
        Set Grenade2=Grenade2-1
        Set GrenadeThrown=2
    Else
        Set Grenade1=Grenade1-1
        Set GrenadeThrown=1
    End
    ActorAnimation masterchief_top_grenade_r Frame=0 AutoFlip
    Goto refresh_ui
ElseIf Control1Fire
    If Actor_AnimationTag == Masterchief_Top_Idle
        
        //Condition to check if we've got an overheated plasma pistol/rifle
        If Weapon1 == Weapon_PlasmaPistol or Weapon1 == Weapon_PlasmaRifle
            If PlasmaOverheated
                Goto reload_start
            End
        End
            
        If Weapon1Clip > 0
            Set PlasmaCharge = 0
            ActorAnimation mc_top_shoots[Weapon1] Frame=0 AutoFlip
            Set Weapon1Clip=Weapon1Clip - 1
            If Weapon1 == Weapon_Rifle
                GoSub fire_rifle
            ElseIf Weapon1 == Weapon_Pistol
                GoSub fire_pistol
            ElseIf Weapon1 == Weapon_PlasmaRifle
                Set PlasmaHeat = PlasmaHeat + 30
            End
            Goto refresh_ui
        Else
            Goto reload_start
        End
            
    End
Else
    ActorAsNew parent
    If Control1_X > 0
        ActorDirection look_right
        NewDirection look_right
    ElseIf Control1_X < 0
        ActorDirection look_left
        NewDirection look_left
    End
    If Actor_AnimationTag == Masterchief_Top_Idle
        Goto reset_mc_animation
    End
End